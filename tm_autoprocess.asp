<!DOCTYPE html>
<html>
<head>
    <!-- #include file="include/connection.asp" -->
    <!-- #include file="include/proc.asp" -->
    <!-- #include file="include/option.asp" -->
    <!-- #include file="include/adovbs.inc" -->
    
    <!-- #include file="tm_process.asp" -->

    <meta http-equiv=Content-Type content='text/html; charset=utf-8'>
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <title>iQOR</title>
    <!-- Tell the browser to be responsive to screen width -->
    <meta content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=no" name="viewport">

    <%
        Server.ScriptTimeout = 10000000
        
        set fs=Server.CreateObject("Scripting.FileSystemObject")
        set fo=fs.GetFolder(Server.MapPath(".") & "\database\attendanceData\")

        for each x in fo.files
          
            sFile = x.Name
            
            a = Split(sFile,".") '=== a = [Data20180224, txt]
            
            sFileInitName = Trim(Mid(a(0),1,4))
            
            if sFileInitName = "Data" Then '==== Only process those files initial is Data

                sDtProcess = Trim(Mid(a(0),5,8)) '==== Get the date out of the file
                sDtProcess = Mid(sDtProcess,7,2) & "/" & Mid(sDtProcess,5,2) & "/" & Mid(sDtProcess,1,4) 
                dtProcess = CDate(sDTProcess)
                
                '===== This is auto processing ============================================================================

                '===== 1. We will read the DataYYYYMMDD.txt file that was generated by EntryPass and insert into TMCLK1
                call fInsertTMCLK1(sFile, sAllEmployee)

                '===== 2. Check Shift Schedule and Insert into TMCLK2======================================================
                call fInsertTMCLK2(dtProcess, sAllEmployee)
        
                '===== 3. Check for OTs, Abnormal, Late, Early, Half Day, Total and TotalOT=================================
                dtCheckFr = dateadd("d", -1, dtProcess) '==== Only process one day before after night shift records are properly inserted
                call fProcAbOT(dtCheckFr, sAllEmployee, "Y") '==== Y means do not process those already have approval/s.
        
                '===== 4. Process one day before because complete night shift data ==========================================
                dtAbsent = dateadd("d", -1, dtProcess)
        
                sSQL = " delete from TMABSENT where DT_ABSENT = '" & fdate2(dtAbsent) & "'"
                if sEmp_Code <> "" then
                    sSQL = sSQL & " and EMP_CODE = '" & sEmp_Code & "'" 
                end if    
                conn.execute sSQL

                call fAbsent(dtAbsent,sAllEmployee)

                '===== 5. Process and record 3 days absents consecutively=====================================================
                call fAbsent3(dtProcess, dtAbsent, sAllEmployee, "N")

                '=============Insert into TMLOG ==============================================================================
                dtProcess = CDate(sDTProcess)
                '===== From Program setup =====
                Set rstTMPATH = server.CreateObject("ADODB.RecordSet")    
                sSQL = "select * from TMPATH" 
                rstTMPATH.Open sSQL, conn, 3, 3
                if not rstTMPATH.eof then
                    sPayFrom = rstTMPATH("PAYFROM") 
                    sPayTo = rstTMPATH("PAYTO")
                end if
                pCloseTables(rstTMPATH)
                
                if Cint(day(dtProcess)) >= Cint(sPayFrom) then 
                    dtProcAb3From = CDate(sPayFrom & "-" & Month(dtProcess) & "-" & Year(dtProcess))
                else
                    dtProcAb3From = CDate(sPayFrom & "-" & GetLastMonth(Month(dtProcess), Year(dtProcess)) & "-" & GetLastMonthYear(Month(dtProcess), Year(dtProcess)))
                end if    
                
                dtProcAb3To = dateadd("d", -1, dtProcess)
        
                sChangesM = "AUTO PROCESS completed on " & Now() & "<br>"
                sChangesM = sChangesM & "Inserted Attendance Records matching their Shift Schedule for date : " & dtProcess & "<br>"
                sChangesM = sChangesM & "Processed Abnormals, OTs and Inserted Absences for date : " & dtCheckFr & "<br>" 
                sChangesM = sChangesM & "Sent out Email Notification to their respective Superior or Managers <br>"
                sChangesM = sChangesM & "Calculated 3 Days Consecutive Absences from " & dtProcAb3From & " to " & dtProcAb3To
      
                sChangesMLog = replace(sChangesM, "<br>", " ")
        
                sSQL = "insert into TMLOG (TYPE,STATUS,REMARK,USER_ID,DATETIME) "
	            sSQL = sSQL & "values ("
                sSQL = sSQL & "'Auto Process',"
                sSQL = sSQL & "'Success',"
                sSQL = sSQL & "'" & sChangesMLog & "',"
                sSQL = sSQL & "'SERVER'," 
                sSQL = sSQL & "'" & fdatetime2(Now()) & "'"
	            sSQL = sSQL & ") "
                conn.execute sSQL

                response.write sChangesM & "<br>"

            End if '=== if sFileInitName = "Data" Then
            
        next 
        set fo=nothing
        set fs=nothing

    %>
</head>
<body>

</body>
</html>
